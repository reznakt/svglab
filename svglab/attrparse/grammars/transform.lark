%import common.WS
%import common.SIGNED_NUMBER

%ignore WS

?start: transform_

// avoid name conflict with lark.Transformer.transform
transform_: (transform_action (","? transform_action)*)?

?transform_action: translate
                 | scale
                 | rotate
                 | skew_x
                 | skew_y
                 | matrix

number: SIGNED_NUMBER

translate: "translate" "(" number (","? number)? ")"
scale: "scale" "(" number (","? number)? ")"
rotate: "rotate" "(" number (","? number ","? number)? ")"
skew_x: "skewX" "(" number ")"
skew_y: "skewY" "(" number ")"
matrix: "matrix" "(" number (","? number)~5 ")"
