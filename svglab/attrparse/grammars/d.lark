%import common.WS
%import common.SIGNED_NUMBER
%ignore WS

?start: path

path: _segment_sequence?
_segment_sequence: m (","? segment)*

?segment: a | c | h | l | m | q | s | t | v | z

?a: absolute_a | relative_a
?c: absolute_c | relative_c
?h: absolute_h | relative_h
?l: absolute_l | relative_l
?m: absolute_m | relative_m
?q: absolute_q | relative_q
?s: absolute_s | relative_s
?t: absolute_t | relative_t
?v: absolute_v | relative_v
z: "Z"i

absolute_a: "A" _arc_args
absolute_c: "C" _cubic_bezier_args
absolute_h: "H" _horizontal_line_args
absolute_l: "L" _line_args
absolute_m: "M" _move_args
absolute_q: "Q" _quadratic_bezier_args
absolute_s: "S" _smooth_cubic_bezier_args
absolute_t: "T" _smooth_quadratic_bezier_args
absolute_v: "V" _vertical_line_args

relative_a: "a" _arc_args
relative_c: "c" _cubic_bezier_args
relative_h: "h" _horizontal_line_args
relative_l: "l" _line_args
relative_m: "m" _move_args
relative_q: "q" _quadratic_bezier_args
relative_s: "s" _smooth_cubic_bezier_args
relative_t: "t" _smooth_quadratic_bezier_args
relative_v: "v" _vertical_line_args

_arc_args: arc (","? arc)*
_cubic_bezier_args: cubic_bezier (","? cubic_bezier)*
_horizontal_line_args: horizontal_line (","? horizontal_line)*
_line_args: line (","? line)*
_move_args: move (","? line)*
_quadratic_bezier_args: quadratic_bezier (","? quadratic_bezier)*
_smooth_cubic_bezier_args: smooth_cubic_bezier (","? smooth_cubic_bezier)*
_smooth_quadratic_bezier_args: smooth_quadratic_bezier (","? smooth_quadratic_bezier)*
_vertical_line_args: vertical_line (","? vertical_line)*

arc: point ","? NUMBER ","? FLAG ","? FLAG ","? point
cubic_bezier: point ","? point ","? point
horizontal_line: NUMBER
line: point
move: point
quadratic_bezier: point ","? point
smooth_cubic_bezier: point ","? point
smooth_quadratic_bezier: point
vertical_line: NUMBER

FLAG: "0" | "1"
NUMBER: SIGNED_NUMBER
point: NUMBER ","? NUMBER
