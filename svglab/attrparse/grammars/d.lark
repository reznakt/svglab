%import common.WS
%import common.SIGNED_NUMBER
%ignore WS

?start: path

path: segment_sequence?
?segment_sequence: m (","? segment)*

?segment: l | h | v | c | a | t | q | s | z | m

?a: "A"i arc (","? arc)*
?c: "C"i cubic_bezier (","? cubic_bezier)*
?h: "H"i horizontal_line (","? horizontal_line)*
?l: "L"i line (","? line)*
?m: "M"i move (","? line)*
?q: "Q"i quadratic_bezier (","? quadratic_bezier)*
?s: "S"i smooth_cubic_bezier (","? smooth_cubic_bezier)*
?t: "T"i smooth_quadratic_bezier (","? smooth_quadratic_bezier)*
?v: "V"i vertical_line (","? vertical_line)*
?z: "Z"i

arc: point ","? NUMBER ","? flag ","? flag ","? point
cubic_bezier: point ","? point ","? point
horizontal_line: NUMBER
line: point
move: point
quadratic_bezier: point ","? point
smooth_cubic_bezier: point ","? point
smooth_quadratic_bezier: point
vertical_line: NUMBER

point: NUMBER ","? NUMBER
?!flag: "0" | "1"

// for some reason, the LALR(1) parser acts up if number is a rule,
// so we'll just make it a terminal and parse it as needed
NUMBER: SIGNED_NUMBER
